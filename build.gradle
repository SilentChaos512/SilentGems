buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

//plugins {
//    id "net.minecraftforge.gradle.forge" version "2.0.2"
//}

version = "2.5.6"
def silentlib_version = "2.2.9"
def buildNum = 0
group = "net.silentchaos512.gems"
archivesBaseName = "SilentsGems-1.12"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.12-14.21.1.2443"
    runDir = "eclipse"

    def versionFile = file('version.properties')
    if (versionFile.canRead()) {
        def Properties versionProps = new Properties()
        
        versionProps.load(new FileInputStream(versionFile))
        
        buildNum = versionProps['BUILD_NUM'].toInteger() + 1
        
        versionProps['BUILD_NUM'] = buildNum.toString()
        versionProps.store(versionFile.newWriter(), null)
    }

    replace "@VERSION@", project.version
    replace "SL_VERSION", silentlib_version
    replace "BUILD_NUM = 0", "BUILD_NUM = " + buildNum
    replaceIn "SilentGems.java"

    mappings = "snapshot_20170624"
}

repositories {
    //mavenCentral()
    maven {
        name = "DVS1 Maven FS"
        url "http://dvs1.progwml6.com/files/maven"
    }
}

jar {
    classifier = buildNum
    manifest {
        attributes "FMLAT": "silentgems_at.cfg"
    }
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = buildNum + '-sources'
    from sourceSets.main.allSource
}
task deobfJar(type: Jar) {
    from(sourceSets.main.output) {
        classifier = buildNum + "-deobf"
    }
}
artifacts {
    archives sourcesJar
    archives deobfJar
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12:4.6.0.65"
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.1.+"
    deobfCompile "slimeknights:TConstruct:1.12-2.7.2.+"
}

sourceSets {
    main {
        java { srcDirs = ["$projectDir/common"] }
        resources { srcDirs = ["$projectDir/resources"] }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}